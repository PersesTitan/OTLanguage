ㅍㅇㅍ

# GET
ㅍㅇㅍ~ㄱㅅㄱ[/] {
    ?ㅅ? ㅅㅆㅅ~? ㅍㅇㅍ~ㄱㅋㄱ name {
        ㅁㄷㅁ 이름::ㅍㅇㅍ~ㄱㅋㄱ name_
    }
} => module~web~index.html

ㅍㅇㅍ~ㄱㅅㄱ[/signup] {
} => module~web~signup.html

ㅍㅇㅍ~ㄱㅅㄱ[/login] {
    ?ㅅ? ㅅㅆㅅ~? ㅍㅇㅍ~ㄱㅋㄱ name {
        ㅁㄷㅁ 이름::ㅍㅇㅍ~ㄱㅋㄱ name_
    }
} => module~web~login.html

# 아이디 저장하는 곳
ㅈㅁㅈ 아이디-비밀번호:{a=1}
ㅈㅁㅈ 아이디-번호

# POST
# 회원가입
ㅍㅇㅍ~ㅍㅅㅍ[/signup][ㅇㅁㅇ 이름:name][ㅇㅁㅇ 비밀번호:password][ㅇㅁㅇ 비밀번호-확인:password_check] {
    ?ㅅ? ㅇㄴ ㅇㅁㅇ~=?[:비밀번호-확인_][:비밀번호_] {
        ㅆㅁㅆ 비밀번호 불일치
    }
    ?ㅈ? ㅇㄴ ~아이디-비밀번호???:이름_ {
        ~아이디-비밀번호 :이름_<<<:비밀번호_
        ~아이디-번호 :~아이디-비밀번호'''_<<<:이름_
        ㅆㅁㅆ :이름_추가 됨
        ㅍㅇㅍ~ㄹㄷㄹ /
    }
    ?ㅉ? {
        ㅆㅁㅆ 이미 존재하는 아이디 입니다.
    }
} => module~web~signup.html

# 로그인
ㅍㅇㅍ~ㅍㅅㅍ[/login][ㅇㅁㅇ 이름:name][ㅇㅁㅇ 비밀번호:password][ㅇㅁㅇ 로그아웃:logout] {
    # 로그아웃 버튼을 눌렀을때
    ?ㅅ? ㅅㅆㅅ~? 로그아웃 {
        # 쿠키 제거
        ㅍㅇㅍ~ㄱㅋㄱ- name
        ㅍㅇㅍ~ㄹㄷㄹ /
    }
    ?ㅈ? ㅇㅁㅇ~=?[:비밀번호_][:~아이디-비밀번호>>>:이름__] {
        # 쿠키 넣기
        ㅍㅇㅍ~ㄱㅋㄱ[name][:이름_]
        ㅍㅇㅍ~ㄹㄷㄹ /
    }
} => module~web~login.html

ㅍㅇㅍ~ㅅㅌㅅ
